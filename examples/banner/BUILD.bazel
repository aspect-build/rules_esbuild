"Demonstrates how to put a banner on output files, see https://esbuild.github.io/api/#banner"

load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@aspect_rules_esbuild//esbuild:defs.bzl", "esbuild")

esbuild(
    name = "banner_newline",
    config = {
        "banner": {
            "js": "//header\n//comments",
        },
    },
    entry_point = "main.js",
)

esbuild(
    name = "banner_quotes",
    config = {
        "banner": {
            "js": "\"use strict\"",
        },
    },
    entry_point = "main.js",
)

esbuild(
    name = "banner_spaces",
    config = {
        "banner": {
            "js": "#!/usr/bin/env node",
        },
    },
    entry_point = "main.js",
)

# Make sed replacements for consistency on different platform
[
    genrule(
        name = "sed_{}".format(basename.replace(".", "_")),
        srcs = [":{}".format(basename)],
        outs = ["sed_{}".format(basename)],
        cmd = "sed \"s/\\/\\/ .*examples\\//\\/\\/ examples\\//\" $(execpath :{}) > $@".format(basename),
    )
    for basename in [
        "banner_newline.js",
        "banner_spaces.js",
        "banner_quotes.js",
    ]
]

write_source_files(
    name = "assertions",
    files = {
        "newline.expected": "sed_banner_newline.js",
        "spaces.expected": "sed_banner_spaces.js",
        "quotes.expected": "sed_banner_quotes.js",
    },
)
